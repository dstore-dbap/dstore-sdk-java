apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    mavenCentral()
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

group = 'io.dstore'

def stdout = new ByteArrayOutputStream()
exec {
    commandLine 'git', 'describe'
    standardOutput = stdout
}
project.setProperty('version', stdout.toString().trim())

jar {
    manifest {
        attributes 'Implementation-Title': name, 'Implementation-Version': version
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

tasks.javadoc.excludes = ['**/procedures/**']

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

// custom tasks for creating a "minimal" jar, which only contains the very basic package
task minimalJar(type: Jar, dependsOn: classes) {
    classifier = 'minimal'
    from sourceSets.main.output
    include 'io/dstore/**'
    exclude 'io/dstore/engine/procedures/**'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

test {
    useTestNG() {
        listeners << 'org.testng.reporters.XMLReporter' // das jenkins-TestNG Plugin basiert darauf!
    }
}

artifacts {
    archives jar
    archives minimalJar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

dependencies {
    compile "io.grpc:grpc-stub:0.9.0"
    compile "io.grpc:grpc-protobuf:0.9.0"
    compile "io.grpc:grpc-auth:0.9.0"
    compile "io.grpc:grpc-netty:0.9.0"
    compile "com.google.protobuf:protobuf-java-util:3.0.0-beta-1"

    testCompile 'org.testng:testng:6.9.6'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.7' //version required
}

// instructions taken from http://central.sonatype.org/pages/gradle.html
uploadArchives {
    repositories {
        mavenDeployer {

            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'dstore-sdk-java'
                packaging 'jar'
                description 'Java SDK for dStore Application Backend '
                url 'https://github.com/dstore-dbap/dstore-sdk-java'

                scm {
                    url 'scm:git@github.com:dstore-dbap/dstore-sdk-java.git'
                    connection 'scm:git@github.com:dstore-dbap/dstore-sdk-java.git'
                    developerConnection 'scm:git@github.com:dstore-dbap/dstore-sdk-java.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'bdolkemeier'
                        name 'Bj√∂rn Dolkemeier'
                        email 'bd@dbap.de'
                    }
                }

            }

        }
    }
}

