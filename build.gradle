apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    mavenCentral()
}

group = 'io.dstore'

def stdout = new ByteArrayOutputStream()
exec {
    commandLine 'git', 'describe'
    standardOutput = stdout
}
project.setProperty('version', stdout.toString().trim())


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

tasks.javadoc.excludes = ['**/procs/**']

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

artifacts {
    archives jar

    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

dependencies {
    compile "io.grpc:grpc-stub:0.9.0"
    compile "io.grpc:grpc-protobuf:0.9.0"
    compile "io.grpc:grpc-auth:0.9.0"
    compile "io.grpc:grpc-netty:0.9.0"
}

// instructions taken from http://central.sonatype.org/pages/gradle.html
uploadArchives {
    repositories {
        mavenDeployer {

            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'dstore-sdk-java'
                packaging 'jar'
                description 'Java SDK for dStore Application Backend '
                url 'https://github.com/dstore-dbap/dstore-sdk-java'

                scm {
                    url 'scm:git@github.com:dstore-dbap/dstore-sdk-java.git'
                    connection 'scm:git@github.com:dstore-dbap/dstore-sdk-java.git'
                    developerConnection 'scm:git@github.com:dstore-dbap/dstore-sdk-java.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'bdolkemeier'
                        name 'Bj√∂rn Dolkemeier'
                        email 'bd@dbap.de'
                    }
                }

            }

        }
    }
}

